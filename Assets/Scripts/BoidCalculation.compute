// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel UpdateBoid

struct Boid 
{
	float3 position;
	float3 velocity;
	float3 acceleration;
	float mass;
	float2 padding;
};

RWStructuredBuffer<Boid> boids;

struct ForceField
{
	float3 position;
	float force;
};

RWStructuredBuffer<ForceField> forceFields;

RWStructuredBuffer<float3> foods;

float deltaTime;

float alignmentForceFactor;
float cohesionForceFactor;
float separationForceFactor;
float foodForceFactor;
float speedForceFactor;
float boundsForceFactor;
float dragCoefficient;
float forceFieldFallofExponent;

float alignmentDistance;
float cohesionDistance;
float separationDistance;
float boundsDistance;
float foodDistance;

float3 simulationCenter;
float3 simulationSize;

float minSpeed;
float maxSpeed;

inline void SetForce(uint id, float3 force);
inline void TimeStep(uint id);
float3 GetFlockingForces(uint id);
float3 GetFoodForce(uint id);
float3 GetSpeedForces(uint id);
float3 GetBoundsForce(uint id);
float3 GetForceFieldForces(uint id);
inline float3 GetSteeringForce(float3 desiredVelocity, Boid boid);
inline float3 GetSeekingForce(float3 desiredPosition, Boid boid);

[numthreads(1024, 1, 1)]
void UpdateBoid (uint3 id : SV_DispatchThreadID)
{
	float3 linearDrag = dragCoefficient * boids[id.x].velocity;
	float3 force = GetFlockingForces(id.x) + GetFoodForce(id.x) + GetForceFieldForces(id.x) + GetSpeedForces(id.x) + GetBoundsForce(id.x) + linearDrag;
	SetForce(id.x, force);
	TimeStep(id.x);
}

inline void SetForce(uint id, float3 force)
{
	boids[id].acceleration = force / boids[id].mass;
}

inline void TimeStep(uint id)
{
	boids[id].velocity += deltaTime * boids[id].acceleration;
	boids[id].position += 0.5f*boids[id].acceleration*deltaTime*deltaTime + boids[id].velocity*deltaTime;
}

float3 GetFlockingForces(uint id)
{
	uint boidsCount;
	uint stride;
	boids.GetDimensions(boidsCount, stride);

	float3 averagePosition = float3(0.0f, 0.0f, 0.0f);
	float3 averageVelocity = float3(0.0f, 0.0f, 0.0f);
	float3 separationForce = float3(0.0f, 0.0f, 0.0f);
	float3 averageFlee = float3(0.0f, 0.0f, 0.0f);

	uint neighbourCount = 0;
	for (uint i = 0; i < boidsCount; i++)
	{
		float neighbourDistance = max(alignmentDistance, max(cohesionDistance, separationDistance));
		float distance = length(boids[i].position - boids[id].position);
		if (distance <= neighbourDistance)
		{
			if (distance < alignmentDistance)
				averageVelocity += boids[i].velocity;

			if (distance < cohesionDistance)
				averagePosition += boids[i].position;

			if (distance < separationDistance && id != i) {
				separationForce += (separationDistance - distance) * (boids[id].position - boids[i].position) / distance;
				averageFlee += (separationDistance - distance) * (boids[id].position - boids[i].position) / distance;
			}

			neighbourCount++;
		}
	}
	averageVelocity /= neighbourCount;
	averagePosition /= neighbourCount;
	averageFlee /= neighbourCount;

	float3 alignmentForce = alignmentForceFactor * GetSteeringForce(averageVelocity, boids[id.x]);
	float3 cohesionForce = cohesionForceFactor * (averagePosition - boids[id.x].position);
	//float3 cohesionForce = cohesionForceFactor * GetSeekingForce(averagePosition, boids[id.x]);
	separationForce *= separationForceFactor;
	//separationForce = separationForceFactor * GetSteeringForce(length(boids[id.x].velocity) * averageFlee, boids[id.x]);
	
	return alignmentForce + cohesionForce + separationForce;
}

float3 GetFoodForce(uint id)
{
	uint foodsCount;
	uint stride;
	foods.GetDimensions(foodsCount, stride);

	float closestDistance = foodDistance;
	float3 closest = float3(0.0f, 0.0f, 0.0f);
	for (uint i = 0; i < foodsCount; i++)
	{
		float distance = length(foods[i] - boids[id].position);
		if (distance < closestDistance)
		{
			closestDistance = distance;
			closest = foods[i];
		}
	}

	if (closestDistance < foodDistance)
		return foodForceFactor * GetSeekingForce(closest, boids[id]);
	return float3(0.0f, 0.0f, 0.0f);
}

float3 GetSpeedForces(uint id)
{
	float velocityNorm = length(boids[id.x].velocity);

	if (velocityNorm > maxSpeed)
		return -20.0f * speedForceFactor * (velocityNorm - maxSpeed) * (boids[id.x].velocity / velocityNorm);
	else if (velocityNorm < minSpeed)
		return 5.0f * speedForceFactor * (minSpeed - velocityNorm) * (boids[id.x].velocity / velocityNorm);
	else return float3(0.0f, 0.0f, 0.0f);
}

float3 GetBoundsForce(uint id)
{
	float3 desiredVelocity = boids[id].velocity;

	float3 x = boids[id].position - simulationCenter;
	float3 minCorner = x + simulationSize / 2;
	float3 maxCorner = simulationSize / 2 - x;

	[unroll(3)]
	for (uint i = 0; i < 3; i++)
	{
		if (minCorner[i] < boundsDistance)
			desiredVelocity[i] = maxSpeed;
		else if (maxCorner[i] < boundsDistance)
			desiredVelocity[i] = -maxSpeed;
	}
	return boundsForceFactor*GetSteeringForce(desiredVelocity, boids[id]);
}

float3 GetForceFieldForces(uint id)
{
	uint fieldsCount;
	uint stride;
	forceFields.GetDimensions(fieldsCount, stride);

	float3 result = float3(0.0f, 0.0f, 0.0f);
	for (uint i = 0; i < fieldsCount; i++)
	{
		float mass = 1;
		float distance = length(forceFields[i].position - boids[id].position);
		if (distance >= 1)
			result += forceFields[i].force * mass / (pow(distance, forceFieldFallofExponent)) * normalize(forceFields[i].position - boids[id].position);
	}
	return result;
}

inline float3 GetSteeringForce(float3 desiredVelocity, Boid boid) 
{
	return desiredVelocity - boid.velocity;
}

inline float3 GetSeekingForce(float3 desiredPosition, Boid boid)
{
	float3 desiredVelocity = maxSpeed * normalize(desiredPosition - boid.position);
	return GetSteeringForce(desiredVelocity, boid);
}
